defcode "exit",4,0,EXIT
	POP_RET H,L		            ; pop return stack into IP
    NEXT_1

defcode "words", 5, 0, WORDS
    SAVE_REGS B,C
    call listWords
    RESTORE_REGS B,C
    NEXT

defcode "find",4,0,FIND
    SAVE_REGS B,C
    POP_PAR0 H,L
    call lookup                     ; params: HL = string address, BC = string length
                                    ; returns: DE = found header ptr or 0
    RESTORE_REGS D,E
    NEXT

defcode "execute",7,0,EXECUTE
    POP_PAR H,L                     ; POP TOS into HL
    jp (hl)                         ; go do Forth word

defcode "number",6,0,NUMBER             ; ( ptr len -- num err )
    SAVE_REGS B,C
    POP_PAR0 H,L
	call toNumber                   ; params: HL = string address, BC = string length
			                        ; Returns the parsed number in DE,
	                                ; BC = 0, success
    PUSH_PAR0 D,E
    RESTORE_REGS B,C
    NEXT

defcode "print",5,0,PRINT
    SAVE_REGS D,E
    call printPStr
    exx
    POP_PAR0 B,C
    NEXT

defcode "token",5,0,TOKEN               ; -- c-addr length  ; NOT STANDARD
    PUSH_PAR0 B,C                         ; save TOS
    exx
    call getToken
    PUSH_PAR0 D,E                         ; DE = str ptr
    RESTORE_REGS B,C                 ; HL = length
    NEXT

defcode "+!",2,0,ADDSTORE               ; (n addr -- )
	POP_PAR H, L		            ; HL = address, TOS = amount to add
	ld A,(HL)
	add A,C                         ; add TOS at (HL),
	ld (HL),A
	inc HL
	ld A,(HL)
	adc A,B
	ld (HL),A
	POP_PAR0 B,C                      ; clear param stack
    NEXT

defcode "[",1,MSK_IMMED,LBRAC
	ld HL, 0
	ld (VAR_STATE), HL	; Set STATE to 0.
    NEXT

defcode "]",1,0,RBRAC
	ld HL, 1
	ld (VAR_STATE), HL	; Set STATE to 1.
    NEXT

defcode "hidden",6,0,HIDDEN
    POP_PAR H,L		            ; HL = Dictionary entry.
    inc HL
    inc HL                      ; Point to name/flags byte.

    ld A, (HL)
    xor MSK_HIDDEN        	    ; Toggle the HIDDEN bit.
    ld (HL), A
    NEXT

; 1+      n1/u1 -- n2/u2            add 1 to TOS
defcode "1+",2,0,ONEPLUS
    inc BC
    NEXT

; 2+      n1/u1 -- n2/u2            add 2 to TOS
defcode "2+",2,0,TWOPLUS
    inc BC
    inc BC
    NEXT

; 1*      n1/u1 -- n2/u2            do nothing
defcode "noop",4,0,noop
    NEXT

defcode "branch",6,0,BRANCH          ; relative branching
    ex DE, HL                   ; add the offset to the instruction pointer
    ld E, (HL)
    inc HL
    ld D, (HL)
    dec HL
    add HL, DE
    NEXT_1

; ?branch   x --              branch if TOS zero
defcode "0branch",7,0,ZBRANCH
    checkZeroWord B,C        ; test old TOS
    POP_PAR0 B,C              ; POP new TOS
    jr z,BRANCH         ; if old TOS=0, branch
    inc DE              ; else skip inline value
    inc DE
    NEXT

; U*   u1 u2 -- ud   unsigned mutliply
defcode "u*",2,0,USTAR  ; u1 u2 -- ud
    SAVE_REGS B,C    ; u2 in BC
    POP_PAR0 D,E          ; u1 in DE
	call unsignedMultiply
    PUSH_PAR0 D,E         ; udlo result
    RESTORE_REGS B,C ; udhi result
    NEXT

;C 0<     n -- flag      true if TOS negative
defcode "0<",2,0,ZEROLESS
    sla b           ; sign bit -> cy flag
    sbc a,a         ;
    and 1            ; <0 = -1, >0 = 0
    ld b,0
    ld c,a
    NEXT

defcode "lit",3,0,LIT

	; IP points to the next command, But in this case it points to the next
	; literal 16 bit integer.  Get that literal increment IP.

	PUSH_PAR0 B,C         ; pushTOS
	ex DE, HL       ; HL = IP
	ld C, (HL)      ; TOS = (IP), IP + 2
	inc HL
	ld B, (HL)
	inc HL
    NEXT_1

;z RSP!  a-addr --                  set return stack pointer
defcode "rsp!",4,0,RSPSTORE
    SET_RET_SP B,C
	POP_PAR0 B,C                      ; POP TOS
    NEXT

defcode "sp@",3,0,SPFETCH
    push IX
    pop HL
    PUSH_PAR H,L
    NEXT

defcode "sp!",3,0,SPSTORE
    POP_PAR H,L
    push HL
    pop IX
    next

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; memory access
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; !        x a-addr --   store cell in memory
defcode "!",1,0,STORE
    POP_PAR H,L
    ld (HL),C
    inc HL
    ld (HL),B
    POP_PAR0 B,C          ; POP new TOS
    NEXT

defcode ",",1,0,COMMA
	ld HL, (VAR_HERE)	; HERE
	ld (HL), C          ; Store DE at memory pointed to by VAR_HERE
	inc HL
	ld (HL), B
	inc HL
	ld (VAR_HERE), HL	; Update HERE (incremented)
	POP_PAR0 B,C		        ; cleanup TOS
    NEXT

defcode "@",1,0,FETCH
    loadWord H,L, B,C ; HL = TOS
	ld C, (HL)      ; TOS = (HL)
	inc HL
	ld B, (HL)
    NEXT

; CELL+    c-addr1 -- c-addr2   add cell size to addr
defcode "cell+",5,0,CELLPLUS
    jp twoplus

; CELLS   ( n1 -- n2 ) n2 is the size in address units of n1 cells.
defcode "cells",5,0,CELLS
    jp twostar

; C!      char c-addr --    store char in memory
defcode "c!",2,0,CSTORE
    POP_PAR H,L
    ld (HL),C
    POP_PAR0 B,C          ; POP new TOS
    NEXT

defcode "c,",1,0,CCOMMA
    PUSH_PAR0 H,L
	ld HL, (VAR_HERE)	; HERE
	ld (HL), C          ; Store DE at memory pointed to by VAR_HERE
	inc HL
	ld (VAR_HERE), HL	; Update HERE (incremented)
    POP_PAR0 H,L
	POP_PAR0 B,C		        ; cleanup TOS
    NEXT

; C@     c-addr -- char   fetch char from memory
defcode "c@",2,0,CFETCH
    ld A,(BC)
    ld C,A
    ld B,0
    NEXT

; CHAR+    c-addr1 -- c-addr2   add char size to addr
defcode "char+",5,0,CHARPLUS
    jp oneplus

; CELLS   ( n1 -- n2 ) n2 is the size in address units of n1 cells.
defcode "chars",5,0,CHARS
    jp noop


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; arithmetic
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; */MOD star-slash-mod

; +       n1/u1 n2/u2 -- n3/u3     add n1+n2
defcode "+",1,0,PLUS
    POP_PAR0 H,L
    add HL,BC
    loadWord B,C, H,L
    NEXT

; 2*      n1/u1 -- n2/u2            arithmetic left shift
defcode "2*",2,0,TWOSTAR
    sla C
    rl B
    NEXT

defcode "-",1,0,MINUS
    POP_PAR0 H,L
    or A
    sbc HL,BC
    loadWord B,C, H,L
    NEXT

; U/MOD   u1 u2 -- u1 u2    unsigned 16 / 16 -> r q
defcode "u/mod",5,0,USLASHMOD
    SAVE_REGS D,E
    POP_PAR0 B,C             ; DE = divisor
                            ; BC = dividend
    call divideWordByWord
    PUSH_PAR0 H,L           ; PUSH remainder,
    RESTORE_REGS B,C
    NEXT

; 2/      x1 -- x2                  arithmetic right shift
defcode "2/",2,0,TWOSLASH
    sra B
    rr C
    NEXT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; logic
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; 0= zero-equals
defcode "0=",2,0,ZEQUAL
    checkZeroWord B,C            ; returns 0 if not equal, 1 if equal
    ; jr z, ZE1
    _if nz
        ld BC, -1
    _endif
    inc BC
    NEXT

; and    x1 x2 -- x3            logical and
defcode "and",3,0,and	        ; bitwise and
    POP_PAR0 H,L
    ld A,b
    and H
    ld B,A
    ld A,C
    and L
    ld C,A
    NEXT

; INVERT x1 -- x2               ; bitwise complement
defcode "invert",6,0,INVERT
    ld A,B
    cpl
    ld B,A
    ld A,C
    cpl
    ld C,A
    NEXT

; TRUE
defcode "true",4,0,TRUE
    PUSH_PAR0 B,C
    ld BC,1
    NEXT

; LSHIFT  x1 u -- x2       logical L shift u places
defcode "lshift",6,0,LSHIFT
    POP_PAR0 H,L              ;   NB: hi 8 bits ignored!
    ld B,C              ; b = loop counter
    inc B               ; test for counter=0 case
    jr LSHIFT2
    _do
        add HL,HL        ; left shift HL, n times
LSHIFT2:
    _djnz
    loadWord B,C, H,L     ; result is new TOS
    NEXT

; =      x1 x2 -- flag         test x1=x2
defcode "=",1,0,EQUALS
    POP_PAR0 H,L
    or A
    sbc HL,BC       ; x1-x2 in HL, SZVC valid
    _if nz
        ld BC, 1       ;   if -ve, result true
    _endif
    inc BC
    NEXT

; <      n1 n2 -- flag          test n1<n2
defcode "<",1,0,LESSTHAN	        ; less than
    POP_PAR0 H,L
    or A
    sbc HL,BC       ; n1-n2 in HL, SZVC valid
    ; jp pe,revsense  ; if OV, use rev. sense
    _if pe
        ; jp m,tosfalse ; OV: if -ve, reslt false
        ; jr tostrue      ;     if +ve, result true
        _if m
            ld bc, 0
        _else
            ld bc, 1       ;   if -ve, result true
        _endif
    _else
        ; jp p,tosfalse   ;   if +ve, result false
        ; jp tostrue
        _if p
            ld bc, 0
        _else
            ld bc, 1       ;   if -ve, result true
        _endif
    _endif
    NEXT

; >      n1 n2 -- flag          test n1 > n2
defcode ">",1,0,GREATERTHAN	        ; less than
    POP_PAR0 H,L                  ; swap operands
    PUSH_PAR H,L
    jr LESSTHAN

; OR    x1 x2 -- x3             logical OR
defcode "or",2,0,OR	            ; bitwise OR
    POP_PAR0 H,L
    ld A,B
    or H
    ld B,A
    ld A,C
    or L
    ld C,A
    NEXT

; xor    x1 x2 -- x3            logical xor
defcode "xor",3,0,xor	        ; bitwise xor
    POP_PAR0 H,L
    ld A,B
    xor H
    ld B,A
    ld A,C
    xor L
    ld C,A
    NEXT

; FALSE
defcode "false",5,0,FALSE
    PUSH_PAR0 B,C
    ld BC,0
    NEXT

;RSHIFT  x1 u -- x2    logical R shift u places
defcode "rshift",6,0,RSHIFT
    POP_PAR0 H,L         ;   NB: hi 8 bits ignored!
    ld B,C              ; b = loop counter
    inc B               ; test for counter=0 case
    jr RSHIFT2
    _do
        srl H           ; right shift HL, n times
        rr L
RSHIFT2:
    _djnz
    loadWord B,C, H,L     ; result is new TOS
    NEXT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; stack
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


defcode "dup",3,0,DUP        ; x -- x x
    PUSH_PAR0 B,C
    NEXT

; SWAP     x1 x2 -- x2 x1    swap top two items
defcode "swap",4,0,SWAP      ; x1 x2 -- x2 x1
    POP_PAR0 H,L
    PUSH_PAR H,L
    NEXT

defcode "rot",3,0,ROT        ; x1 x2 x3 -- x2 x3 x1
    SAVE_REGS B,C             ; x3 in BC
    POP_PAR0 H,L              ; x2 in HL
    POP_PAR0 D,E              ; x1 in DE
    EX DE,HL                 ; x1 in HL, x2 in DE
    PUSH_PAR0 D,E             ; x2 on stack
    PUSH_PAR H,L             ; PUSH x1 - x2 x3 x1
    RESTORE_REGS B,C          ; x1 in BC
    NEXT

defcode "-rot",4,0,NROT        ; x1 x2 x3 -- x3 x1 x2
    SAVE_REGS B,C             ; x3 in BC
    POP_PAR0 H,L              ; x2 in HL
    POP_PAR0 D,E              ; x1 in DE
    PUSH_PAR D,E             ; PUSH x1 - x3 x1
    PUSH_PAR H,L             ; PUSH x2 - x3 x1 x2
    RESTORE_REGS B,C          ; x2 in BC
    NEXT

; >R    ( x -- ) ( R: -- x )  Move  x from TOS to the return stack.
defcode ">r",2,0,TO_R
    PUSH_RET B,C
    POP_PAR0 B,C
    NEXT

; R@ r-fetch

defcode "drop",4,0,DROP      ; x --
    POP_PAR0 B,C		    ; drop top of stack
    NEXT

; OVER    x1 x2 -- x1 x2 x1
defcode "over",4,0,OVER
    POP_PAR0 H,L
    PUSH_PAR0 H,L
    PUSH_PAR H,L
    NEXT

; R>    ( -- x ) ( R: x -- )  Move x from the return stack to TOS
defcode "r>",2,0,R_FROM
    PUSH_PAR0 B,C
    POP_RET B,C
    NEXT

defcode "'",1,0,TICK                 ; ( -- addr ) PUSH code word ptr of following word
    jp LIT

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; definitions
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; CREATE     addr len --       create new header from name
defcode "create",6,0,CREATE  ; addr len --

    SAVE_REGS B,C
    POP_PAR0 H,L
	; Link pointer.
	ld DE, (VAR_HERE)	    ; DE point to the start of a new header
	ld A, (VAR_LATEST) 	    ; write ptr to previous word in header
    ld (DE), A
    inc DE
    ld A, (VAR_LATEST + 1)
    ld (DE), A
    inc DE

	; Length byte and the word itself.
	ld A, C		    ; Get the length of namw.
	ld (DE), A      ; write to length/flags field of header
    inc DE          ; HL = name, de = name field in header, BC = length
	ldir            ; copy BC bytes from (HL) to (DE)

	; Update LATEST and HERE.
	ld HL, (VAR_HERE)
	ld (VAR_LATEST), HL
	ld (VAR_HERE), DE

    exx
    POP_PAR0 B,C
    NEXT

defcode "immediate",9,MSK_IMMED,IMMEDIATE
    ld HL, (VAR_LATEST)                 ; get head of latest word
    inc HL                              ; point to flags byte
    inc HL
    ld a, MSK_IMMED
    xor (HL)                            ; toggle the IMMED bit.
    ld (HL),a
    NEXT


; DOES> does

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; device
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

defcode "accept",6,0,ACCEPT              ; ( addr max -- len )
    SAVE_REGS D,E                    ; max - up to 255
	POP_PAR0 H,L                          ; addr
    call getline
    RESTORE_REGS B,C                 ; TOS = len
    NEXT

defcode "key",3,0,KEY                ; ( -- c )
	call getKey
    ld b, 0
    ld c, a
    NEXT

defcode "emit",4,0,EMIT              ; print the character on the stack
	ld A, C                     ; (c -- )
	call putc
    POP_PAR0 B,C                      ; clear stack
    NEXT

defcode "key?",4,0,QKEY              ; ( -- c )
	PUSH_PAR0 B,C		                ; save TOS
	call inputReady
    ld BC, 1                    ; TOS = true
    _if z
        ld BC, 0                    ; TOS = false
    _endif
    NEXT

defcode ".",1,0,DOT                 ; print top of stack in BASE
    call printNum
    POP_PAR0 B,C
    NEXT

defcode ".s",2,0,DOTS                  ; ( -- )
    call printStack
    call printNum                   ; print tos
    NEXT

defcode "type",4,0,TYPE             ; ( str len -- )
    push DE
    POP_PAR0 D,E                    ; DE = str BC = length
    call printStr
    POP_PAR0 B,C                    ; clear TOS
    pop DE
    NEXT